Dim rc // result code
Dim advReport$
Dim scanReport$
Dim peerAddr$
Dim connection

rc = gpiosetfunc(15, 2, 2)
rc = gpiosetfunc(17, 2, 2)

// initialize services
rc = BleGapSvcInit("swing", 0, 256, 500000, 1000000, 4000000, 0)
//
Dim mdVal As Integer
Dim mdCccd As Integer
Dim attr$ As String
Dim hThermoService As Integer
rc = BleSvcCommit(1, BleHandleUuid16(0xfff0), hThermoService)
Dim hThermoValue As Integer
mdVal  = BleAttrMetaData(0, 1, 1, 0, rc)
rc = BleCharNew(0x04, BleHandleUuid16(0xfff1), mdVal, 0, 0)
rc = BleEncode8(attr$, 0xff, 0)
rc = BleCharCommit(hThermoService, attr$, hThermoValue)
Print "\nCommit: ", rc
Print "\nhThermoValue: ", hThermoValue

// start advertisement
advReport$ = ""
scanReport$ = ""
peerAddr$ = ""
rc = BleAdvRptInit(advReport$, 0x06, 1, 10)
rc = BleAdvRptsCommit(advReport$, scanReport$)
rc = BleAdvertStart(0, peerAddr$, 100, 0, 0)

// Event handlers
Function HandleBleMessage(ByVal msgId As Integer, ByVal ctx As Integer) As Integer
    Select msgId
    Case 0
        Print "\nConnect ", ctx
        rc = BleAuthenticate(ctx)
        connection = ctx
        rc = BleAdvertStop()
        // start update timer
        timerStart(0, 500, 1)
    Case 1
        Print "\nDisconnect ", ctx
        connection = 0
        // stop update timer
        timerCancel(0)
        // start advertisement
        rc = BleAdvertStart(0, peerAddr$, 100, 0, 0)
    Case Else
        Print "\nUnknown message ", msgId
    EndSelect
EndFunc 1
OnEvent EVBLEMSG Call HandleBleMessage

Function HandleCharVal(ByVal charHandle As Integer) As Integer
    Dim value$, sw
    If charHandle == hThermoValue Then
        value$ = "\00"
        rc = BleCharValueRead(charHandle, value$)
        If rc == 0 Then
            rc = BleDecodeU8(value$, sw, 0)
            If sw != 0 Then
                gpiowrite(15, 1)
                gpiowrite(17, 1)
            Else
                gpiowrite(15, 0)
                gpiowrite(17, 0)
            EndIf
        EndIf
    EndIf
EndFunc 1
OnEvent EVCHARVAL Call HandleCharVal

Function HandleBleCccd(ByVal charHandle As Integer, ByVal value As Integer) As Integer
    If charHandle == hThermoValue Then
        PRINT "\nCCCD:", value
    EndIf
EndFunc 1
OnEvent EVCHARCCCD Call HandleBleCccd


// Wait events
WaitEvent
